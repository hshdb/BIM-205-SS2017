#
# SPARQL query to harvest basic data about vocabularies from LOV 
# via endpoint <http://lov.okfn.org/dataset/lov/sparql>.
#

PREFIX vann: <http://purl.org/vocab/vann/>
PREFIX voaf: <http://purl.org/vocommons/voaf#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct:  <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

# TODO:
# - author
# - languages
# - license

SELECT ?prefixes ?titles ?uri ?url ?abstract ?size ?year ?keywords {
    
    ?uri a voaf:Vocabulary .

    # prefixes
    {
        SELECT ?uri (GROUP_CONCAT(?prefix;separator="; ") as ?prefixes) {
            ?uri vann:preferredNamespacePrefix ?prefix .
        } GROUP BY ?uri
    }

    # homepage url (just a random one)
    OPTIONAL {
        SELECT ?uri (SAMPLE(?u) as ?url) {
            ?uri foaf:homepage ?u
        } GROUP BY ?uri
    }

    # English title(s)
    {
        SELECT ?uri (GROUP_CONCAT(?title;separator="; ") as ?titles) {
            ?uri dct:title ?title . FILTER (lang(?title) = 'en') 
        } GROUP BY ?uri
    }

    # English abstract (just a random one)
    OPTIONAL {
        SELECT ?uri (SAMPLE(?abs) as ?abstracts) {
            ?uri dct:description ?abs . FILTER (lang(?abs) = 'en')
        } GROUP BY ?uri
    }

    # keywords
    {
        SELECT ?uri (GROUP_CONCAT(?keyword;separator="; ") as ?keywords) {
            ?uri dcat:keyword ?keyword
        } GROUP BY ?uri
    }

    # size (FIXME)
    {
        SELECT (CONCAT(
                STR(?classes), " classes, ",
                STR(?properties), ' properties, ',
                STR(?instances), ' instances (',
                STR(YEAR(?date)) ,')'
               ) AS ?size)
        {
            ?uri dcat:distribution ?distr . 
            ?distr dct:issued ?date .
            ?distr voaf:classNumber ?classes .
            ?distr voaf:propertyNumber ?properties .
            ?distr voaf:instanceNumber ?instances .
            # TODO: dct:language
        } ORDER BY DESC(?date) LIMIT 1
    }

    # year of creation (FIXME)
    {
        SELECT (YEAR(?creation) AS ?year) {
            ?uri dcat:distribution [ dct:issued ?creation ] .
        } ORDER BY ASC(?creation) LIMIT 1
    }
} 
